function prompt_zyndiecate_precmd {
  git-info
}



# Show current directory name as iterm tab title
function prompt_cwd_in_tab {
  echo -ne "\033]0;$(basename $(echo $PWD))\007"
}



# Show vi mode indicator
function zle-line-init zle-keymap-select {
  RPS1="%B%F{red}${${KEYMAP/vicmd/<<<}/(main|viins)/}%f%b"
  RPS2=$RPS1
  zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select



# Start in vi command mode
#zle-line-init() { zle -K vicmd; }
#zle -N zle-line-init



function prompt_zyndiecate_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_zyndiecate_precmd
  add-zsh-hook precmd prompt_cwd_in_tab

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{red}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{white}-%f $(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s %F{white}-%f' \
    'rprompt' '%C%D'

  # Define prompts.
  PROMPT='%F{green}%p%n@%M%f${(e)git_info[prompt]}%f $ '
  RPROMPT=''
}

prompt_zyndiecate_setup "$@"
